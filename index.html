<html>
    <head>
        <title>Baabnq</title>
        <link rel="stylesheet" href="style.css">
    </head>
    <body>
    <div class="page">
        <h1>The Baabnq Programming Language</h1>
        
        
        <h2>Grammar</h2>
        <pre class="embed">
program ::= unit*
unit ::= ( statement eos | comment ) ["\n"]

eos ::= ";"

comment ::= /".+?/

statement ::= 
    put     |
    input   |
    print   |
    putchr  |
    lab     |
    jump    |
    sub     |
    return  |
    pull    |
    push    |
    new     |
    free    |
    static  |
    use     |
    asm
    

<a href="statements/put.html">put</a> ::= "put" (
    var  "="  expr |
    var  "<-" expr |
    expr "->" expr )

<a href="statements/input.html"     >input</a>  ::= "input"   var
<a href="statements/print.html"     >print</a>  ::= "print"   expr
<a href="statements/putchr.html"    >putchr</a> ::= "putchr"  expr
<a href="statements/lab.html"       >lab</a>    ::= "lab"     label
<a href="statements/jump.html"      >jump</a>   ::= "jump"    label [cond]
<a href="statements/sub.html"       >sub </a>   ::= "sub"     label [cond]
<a href="statements/return.html"    >return</a> ::= "return"
<a href="statements/pull.html"      >pull</a>   ::= "pull"    var
<a href="statements/push.html"      >push</a>   ::= "push"    expr
<a href="statements/new.html"       >new</a>    ::= "new"     (expr | string) var
<a href="statements/static.html"    >static</a> ::= "static"  (expr | string) var
<a href="statements/free.html"      >free</a>   ::= "free"    var
<a href="statements/use.html"       >use</a>    ::= "use"     string
<a href="statements/asm.html"       >asm</a>    ::= "asm"     string


expr ::=
  number    |
  var       |
  binary    |
  paran

binary ::= expr op expr
paran  ::= "(" expr ")"

op ::= 
    "+"  | 
    "-"  | 
    "|"  | 
    "&"  |
    "^"  | 
    "<<" |
    ">>"

cond ::= "~" expr ('==' | '!=' | '>' | '<') expr


label   ::= iden
string  ::= /'[^']*'/
var     ::= "_" | iden
iden    ::= /[^'|;|\s|(|)]+/
number  ::= /\d+/
        </pre>


    </div>
    </body>
</html>